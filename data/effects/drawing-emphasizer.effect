uniform float4x4 ViewProj;
uniform texture2d image;

sampler_state def_sampler {
	Filter   = Linear;
	AddressU = Clamp;
	AddressV = Clamp;
};

struct VertInOut {
	float4 pos : POSITION;
	float2 uv  : TEXCOORD0;
};

VertInOut VSDefault(VertInOut vert_in)
{
	VertInOut vert_out;
	vert_out.pos = mul(float4(vert_in.pos.xyz, 1.0), ViewProj);
	vert_out.uv  = vert_in.uv;
	return vert_out;
}

float4 PSDraw(VertInOut vert_in) : TARGET
{
	return image.Sample(def_sampler, vert_in.uv);
}

float4 PSExtractLuminance(VertInOut vert_in) : TARGET
{
	float4 color = image.Sample(def_sampler, vert_in.uv);
	float luminance = dot(color.rgb, float3(0.299, 0.587, 0.114));
	return float4(luminance, luminance, luminance, 1.0);
}

float4 PSDetectEdge(VertInOut vert_in) : TARGET
{
	float2 texel_size = float2(length(ddx(vert_in.uv)), length(ddy(vert_in.uv)));
	float2 uv = vert_in.uv;

	float lum[9];
	lum[0] = image.Sample(def_sampler, uv + texel_size * float2(-1.0, -1.0)).r;
	lum[1] = image.Sample(def_sampler, uv + texel_size * float2( 0.0, -1.0)).r;
	lum[2] = image.Sample(def_sampler, uv + texel_size * float2( 1.0, -1.0)).r;
	lum[3] = image.Sample(def_sampler, uv + texel_size * float2(-1.0,  0.0)).r;
	lum[4] = image.Sample(def_sampler, uv).r;
	lum[5] = image.Sample(def_sampler, uv + texel_size * float2( 1.0,  0.0)).r;
	lum[6] = image.Sample(def_sampler, uv + texel_size * float2(-1.0,  1.0)).r;
	lum[7] = image.Sample(def_sampler, uv + texel_size * float2( 0.0,  1.0)).r;
	lum[8] = image.Sample(def_sampler, uv + texel_size * float2( 1.0,  1.0)).r;

	float sobel_x = -lum[0] - 2.0 * lum[3] - lum[6] + lum[2] + 2.0 * lum[5] + lum[8];
	float sobel_y = -lum[0] - 2.0 * lum[1] - lum[2] + lum[6] + 2.0 * lum[7] + lum[8];

	float edge = sqrt(sobel_x * sobel_x + sobel_y * sobel_y);

	return float4(edge, edge, edge, 1.0);
}

technique Draw
{
	pass
	{
		vertex_shader = VSDefault(vert_in);
		pixel_shader  = PSDraw(vert_in);
	}
}

technique ExtractLuminance
{
	pass
	{
		vertex_shader = VSDefault(vert_in);
		pixel_shader  = PSExtractLuminance(vert_in);
	}
}

technique DetectEdge
{
	pass
	{
		vertex_shader = VSDefault(vert_in);
		pixel_shader  = PSDetectEdge(vert_in);
	}
}
